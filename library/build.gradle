apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.getkeepsafe.dexcount' // 统计方法数

repositories {
    flatDir { dirs 'libs' }
    mavenCentral()
    google()
    jcenter()
    maven { url "https://jitpack.io" }
}

group = 'com.github..zhaoxiuyu'

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi', 'armeabi-v7a' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            buildConfigField("boolean", "IS_DEBUG", "false")
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            buildConfigField("boolean", "IS_DEBUG", "true")
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        unsigned {
            minifyEnabled false
            buildConfigField("boolean", "IS_DEBUG", "false")
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        androidTest.resources.srcDirs += 'src/androidTest/res'
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    api 'androidx.cardview:cardview:1.0.0'
    api 'androidx.multidex:multidex:2.0.1'
    api 'androidx.appcompat:appcompat:1.0.2' //v7
    api 'androidx.legacy:legacy-support-v4:1.0.0'  //v4
    api 'androidx.recyclerview:recyclerview:1.0.0'
    api 'com.google.android.material:material:1.0.0' //design
    api 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Room
    api 'org.litepal.android:kotlin:3.0.0'

    // 工具类
    api 'com.blankj:utilcode:1.25.9'//柯基 AndroidUtilCode

    // MMV
    api 'com.tencent:mmkv:1.0.23'

    // Kotlin
    api 'androidx.core:core-ktx:1.1.0'
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // 内存泄漏检测工具
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-2'

    // 性能检测工具
    debugApi 'com.didichuxing.doraemonkit:doraemonkit:1.2.1'
    releaseApi 'com.didichuxing.doraemonkit:doraemonkit-no-op:1.2.0'

    // 网络请求核心库,rx扩展,rx下载,rxandroid
    api 'com.lzy.net:okgo:3.0.4'
    api 'com.lzy.net:okrx2:2.0.2'
    api 'com.lzy.net:okserver:2.0.5'
    api 'io.reactivex.rxjava2:rxandroid:2.1.1'
    api 'com.squareup.retrofit2:retrofit:2.6.1'
    api 'com.squareup.retrofit2:converter-scalars:2.6.1'
    api 'com.squareup.retrofit2:adapter-rxjava2:2.6.1'

    // 解决RxJava内存泄漏
    api 'com.uber.autodispose:autodispose:1.3.0'
    api 'com.uber.autodispose:autodispose-android-archcomponents:1.3.0'

    // 图片配置
    api 'com.github.bumptech.glide:glide:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'
    api 'com.github.bumptech.glide:okhttp3-integration:4.9.0'
    api 'jp.wasabeef:glide-transformations:4.1.0'

    // View动画效果大集合
    api 'com.daimajia.easing:library:2.0@aar'
    api 'com.daimajia.androidanimations:library:2.3@aar'

    // 动画工具类
    api 'com.github.florent37:viewanimator:1.1.0'

    // Json相关
    api 'com.google.code.gson:gson:2.8.5'

    // 适配器
    api 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.45-androidx'

    // 分割线
    api 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    api 'com.oushangfeng:PinnedSectionItemDecoration:1.3.2-androidx'

    // banner 轮播图
    api 'com.youth.banner:banner:1.4.10'

    // 提示框
    api 'com.lxj:xpopup:1.8.10-x'

    // SuperTextView
    api 'com.github.lygttpod:SuperTextView:2.2.4'

    //lottie
    api 'com.airbnb.android:lottie:3.0.1'

    //手势密码
    api 'com.github.ihsg:PatternLocker:2.5.4'

    implementation 'com.tencent.bugly:crashreport:3.1.0'
    implementation 'com.tencent.bugly:nativecrashreport:3.7.1'

}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task classesJar(type: Jar) {
    from "$buildDir/intermediates/classes/release"
}

artifacts {
    archives classesJar
    archives javadocJar
    archives sourcesJar
}